! final_clean_box -- special version of 'final_clean_rms' script
! 		      to use windows for part of the CLEAN and then
!                     clean without windows for the remainder of
!                     the CLEAN as normal.
!
! Version: 2012_08_02
! Change Log:
!    2012_08_02 --  Discovered that the windowed clean does not always 
!                   interact as expected with the dynamic range cutoff
!                   criteria in early rounds of cleaning (if there is
!                   a bright spot of noise outside windows).  So now
!                   pre-defining the absolute map peak at the start of
!                   the clean to always be the reference location for the
!                   dynamic range test	
!    2012_07_11 --  Added no improvement critera for stopping boxed part
!                   of CLEAN to avoid over-cleaning with boxes, even with
!                   the increased target RMS value during boxed CLEAN. 
!    		--  Added the target RMS factor and window file to the 
!                   command line as input parameters %2 and %3, so it 
!                   should be run with the command
!
!		    @final_clean_box i, box_rms_factor, window_file
!
!                   where "i" is replaced by stokes parameter of interest,
!		    "box_rms_factor" is replaced a number to multiply the
!                   target rms by as a limit for keeping boxes, currently
!                   4.0 seems good, but it may vary by source,
!                   and "window_file" is replaced by the name of the 
!                   boxes file to use
!
!    2012_07_10 - added command to load and use cleaning boxes until we
!                   reach 4.0 times target RMS
!    2011_05_09 - limit maximum clean iter in natural clean to 500
!
! Script revised -- May 2011 by D. Homan to improve stopping
! criteria and not overclean images.   See 'deep_map_residual'
! macro below for how this works in detail.  The idea is to
! compare the residual map rms in the frame to a target RMS which
! is either calculated from the calibrated weights in the image or
! from the residual in the unclead V map and stop when the 
! in_frame rms is less than the target value.
!
! The final in frame rms in the restored map 
! should then be reasonably close to the target value; however,
! test show there is still typically a ~10% reduction in the
! restored map in_frame rms (in a corner) compared to the
! the far frame rms, so it is still advisable to use the
! far frame rms for uncertainty calculations, etc.  The
! exception to this is the rare source where the cleaning
! is stopped by the second criteria which is no improvement
! in the in_frame rms. In that case, it is probably best to
! use the largest of the in_frame and far_frame rms values in
! the restored map for uncertainty calculations, contouring, etc...
!
! Notes for the original version of the script below.
!-------------------------------------------------------------
! final_clean -- script for making deeply cleaned images
!                by Dan Homan,  dhoman@nrao.edu
!
! This started out as the automatic calibration and mapping
! script by Taylor and Shepard, but I have turned it into
! a multi-resolution cleaning script for producing "final"
! deeply cleaned images in difmap.  No self-calibration
! is performed by this procedure... its' only purpose is
! to perform a deep multi-resolution clean on calibrated data.
!
! The motivation for this procedure comes from tests
! run by George Moellenbrock which showed that 'super-resolving'
! the highest brightness temperature features early on in
! the cleaning process often lead to a much more accurate
! clean of the lower surface brightness features in later
! clean iterations at lower resolution.  'super-resolving'
! in the early rounds seemed to avoid unrecoverable errors
! in cleaning the most compact VLBI structures.
! (Moellenbrock, Ph.D. Thesis (1999))
!
! During the self-calibration process, I usually implement
! George's ideas with carefully controlled 'super-resolution'
! or by modelfitting of the VLBI cores.  For making final, deep
! cleaned images I created this automated (reproducable)
! procedure.
!
! To run it: load the data, set your mapsize, then just type
!
! 0> @final_clean i
!
! Where "i" can be replaced by another Stokes parameter.
! Any exisiting clean components are automatically deleted, 
! uvtapers are removed, and clean windows are deleted.  
! Of course, you can change this by editing the script.
!
! The lowest resolution used is simply 'natural
! weighting' of "uvweight 0,-2".  Convolving with
! the same beam as this lowest resolution is probably
! the safest thing to do.  As 'natural weighting' is
! is the last weighting used by the procedure, the
! model will be automatically convolved with 
! this beam unless a specific change of beam or
! resolution is made first.
!
! The procedure just cleans, so you must write the
! FITS image out of difmap yourself...
!
! 0> wmap filename
! 
! Of course the model components (clean components) can
! be saved separately if desired...
!
! 0> wmod filename.mod
!


integer clean_niter; 
float clean_gain; clean_gain = 0.03
float dynam;
float flux_peak;
float peak_x;
float peak_y;

! Define the inner loop as a macro.

float flux_cutoff
float dyn_range
float last_in_rms
float in_rms
float map_max
float map_min
float last_map_max
float last_map_min
float box_rms_factor
float target_rms
float V_rms

#+map_residual \
flux_peak = mapvalue(peak_x,peak_y);\
flux_cutoff = imstat(rms) * dynam;\
while(abs(flux_peak)>flux_cutoff);\
 clean clean_niter,clean_gain;\
 flux_cutoff = imstat(rms) * dynam;\
 flux_peak = mapvalue(peak_x,peak_y);\
end while

! The following macro stops
! when the the in_frame RMS matches the
! the V RMS OR if there is not improvement 
! in the in_frame RMS

#+deep_map_residual \
in_rms = imstat(rms);\
print "Target RMS: ", target_rms, "  In Frame RMS: ", in_rms;\
while(in_rms > target_rms);\
 clean min(100*(in_rms/target_rms),500),clean_gain;\
 last_in_rms = in_rms;\
 in_rms = imstat(rms);\
 print "Target RMS: ", target_rms, "  In Frame RMS: ", in_rms;\
 if(last_in_rms <= in_rms);\
  in_rms = target_rms;\
 end if;\
end while

! The following macro is very similar
! to the above macro but it adds additional
! stopping criteria if there is no improvement
! in the max or min in the image to avoid overcleaning
! when boxes are in place.
!
! NOTE that this macro also moves the two pre-loop
! commands to the main part of the script to keep the
! length of the macro short enough for memory space

#+deep_map_residual_box \
while(in_rms > target_rms);\
 clean min(100*(in_rms/target_rms),500),clean_gain;\
 last_in_rms = in_rms;\
 last_map_max = map_max;\
 last_map_min = map_min;\
 in_rms = imstat(rms);\
 map_max = peak(flux,max);\
 map_min = peak(flux,min);\
 print "Boxed Target RMS: ", target_rms, "  In Frame RMS: ", in_rms;\
 if(last_in_rms <= in_rms | last_map_max <= map_max | last_map_min >= map_min);\
  in_rms = target_rms;\
 end if;\
end while


! select V and get the V_rms for comparison
select v
clrmod true,true,true
uvw 0,-2
delwin
uvtaper 0
V_rms = imstat(rms)

! select the stokes to clean
select %1

! set the box rms factor
box_rms_factor = %2

! load the clean boxes to be use
rwin %3

! clear previous model
clrmod true,true,true

! remove any tapering
uvtaper 0

print "******* FIRST TRY SUPER-UNIFORM WEIGHTING (BOXED) *******"
print "**** -- only if dynamic range is higher than 10 -- *****"

! Define fixed peak location from which 
!  dynamic range stopping criteria will
!  be measured.  This modification is 
!  necessary to accomodiate clean windows
!  where the peak(flux) value could come
!  from outside the clean area 
peak_x = peak(x);
peak_y = peak(y);

dynam = 10
clean_niter = 10
uvw 20,-1
map_residual
uvw 10,-1
map_residual
clean_niter = 50

print "******** REGULAR UNIFORM WEIGHTING NEXT (BOXED) ***********"
uvw 2,-1
dynam = 6
map_residual

print "********** BOXED CLEANING AT NATURAL WEIGHTING ************"
uvw 0,-2
! first clean with boxes until boxed_rms_factor x target rms 
!  limit is reached then delete boxes
target_rms = box_rms_factor*imstat(noise)
in_rms = imstat(rms)
map_max = peak(flux,max)
map_min = peak(flux,min)
print "Boxed Target RMS: ", target_rms, "  In Frame RMS: ", in_rms;
deep_map_residual_box

! Now delete windows for rest of cleaning
delwin

print "****** DEEP CLEANING AT NATURAL WEIGHTING (UNBOXED) *******"
uvw 0,-2
! now let clean go deep
target_rms = imstat(noise)
if(target_rms < imstat(rms))
  deep_map_residual
else
  ! clean 1 component just to have something to restore
  clean 1, clean_gain
end if

in_rms = imstat(rms)
print "********** FINAL CLEAN IS FINISHED **************"
print "Target RMS was: ", target_rms, " Reached RMS: ", in_rms
print "For comparison uncleaned V RMS is: ", V_rms
print "*************************************************"



